[gd_resource type="ShaderMaterial" load_steps=2 format=3 uid="uid://chj6q0qoon21l"]

[sub_resource type="Shader" id="1"]
code = "shader_type canvas_item;
// FROM: https://wiki.orx-project.org/en/tutorials/shaders/hexagongrid2

uniform vec2 gridSize;
uniform vec2 mapSize;
uniform vec2 containerSize;
uniform sampler2D hexColors;

const float SQRT_2 = 1.4142135623730951;
const float SQRT_3 = 1.73205080757;

// orientation
const float or_f0 = 3.0 / 2.0;
const float or_f1 = 0.0;
const float or_f2 = SQRT_3 / 2.0;
const float or_f3 = SQRT_3;
const float or_b0 = 2.0 / 3.0;
const float or_b1 = 0.0;
const float or_b2 = -1.0 / 3.0;
const float or_b3 = SQRT_3 / 3.0;
const float or_start_angle = 0.0;

const float layout_origin_x = 16.5;
const float layout_origin_y = 18.5;
const float layout_size_x = 16.666;
const float layout_size_y = 16.165;
 
// Function to convert cubial coords to axial coords.
vec2 cubeToAxial(vec3 cube) {
	return vec2(cube.x, cube.y);
}
 
// Function to convert axial coords to cubial coords.
vec3 axialToCube(vec2 axial) {
	float x = axial.x;
	float y = axial.y;
	float z = -x - y;
	return vec3(x, y, z);
}

vec2 axialToOffset(vec2 hex) {
	float col = hex.x;
	float row = hex.y + (hex.x - (float(int(hex.x) & 1))) / 2.0;
	return vec2(col, row);
}

vec2 offsetToAxial(vec2 hex) {
	float q = hex.y;
	float r = hex.x - (hex.y - (float(int(hex.y) & 1))) / 2.0;
	return vec2(q, r);
}
 
// Function to round float cubial coords to int cubial coords.
vec3 cubeRound(vec3 cube) {
	float rx = round(cube.x);
	float ry = round(cube.y);
	float rz = round(cube.z);
 
	float xDiff = abs(rx - cube.x);
	float yDiff = abs(ry - cube.y);
	float zDiff = abs(rz - cube.z);
 
	if ((xDiff > yDiff) && (xDiff > zDiff))
		rx = -ry - rz;
	else if (yDiff > zDiff)
		ry = -rx - rz;
	else
		rz = -rx - ry;

	return vec3(rx, ry, rz);
}
 
// Function to round float axial coords to int axial coords.
vec2 axialRound(vec2 axial) {
	return cubeToAxial(cubeRound(axialToCube(axial)));
}
 
// Function to return axial hex-grid coords, given a screen position (vertical, flat-top hex layout).
vec2 pixelToHex(vec2 point) {
	vec2 pt = vec2((point.x - layout_origin_x) / layout_size_x, (point.y - layout_origin_y) / layout_size_y);
	float q = or_b0 * pt.x + or_b1 * pt.y;
	float r = or_b2 * pt.x + or_b3 * pt.y;
	float s = -q - r;
	return cubeToAxial(cubeRound(vec3(q, r, s)));
}
 
// Function to return a screen position, given axial hex-grid coords (vertical, flat-top hex layout).
vec2 hexToPixel(vec2 hex) {
	float x = (or_f0 * hex.x + or_f1 * hex.y) * layout_size_x;
	float y = (or_f2 * hex.x + or_f3 * hex.y) * layout_size_y;
	return vec2(x + layout_origin_x, y + layout_origin_y);
}

varying vec2 point;

void vertex(){
    point = (VERTEX / containerSize) * mapSize;
}

void fragment() {
	vec2 hex = axialRound(pixelToHex(point));
	vec2 offset = axialToOffset(hex);
 
	vec4 color;
	if (offset.x < 0.0 || offset.y < 0.0 || offset.x >= gridSize.x || offset.y >= gridSize.y) {
		color = vec4(0.0, 0.0, 0.0, 0.0);
	} else {
		color = texelFetch(hexColors, ivec2(int(offset.x), int(offset.y)), 0);
	}

	COLOR = color;
}"

[resource]
shader = SubResource("1")
shader_parameter/containerSize = null
shader_parameter/gridSize = null
shader_parameter/mapSize = null
